{%@import macrodoc.jim%}
{%MACRO name="for" since={%RELEASE:ZURICH%}%}


== Syntax
The macro `for` can be used to create repetitive text.
It has two forms.
The syntax of the macro is either

{%sample/
{@for variable in (a,b,c,...,d)= content to be repeated
containing variable}
%}

or

{%sample/
{@for (v1,v2,...,vN) in (a|w|...|1,b|q|...|2,c|r|...|5,d|t|...|9)= content to be repeated
containing v1 v2 and v3}
%}



== History

- since {%RELEASE:SZEGED%} multi-argument `for`
- since 1.6.3 backtick string separator value list
- since 1.7.3 options between `[` and `]`
- since 1.7.8 option `evalist`


== Description

The `variable` or the multiple variables can be used in the __content__ and will be replaced for each iteration with the respective element on the comma-separated list.
When there are multiple variables, then the sub-list of the values is separated using the `|` character.
Both the `,` and the `|` character can be modified to use something else instead of these characters.

The list of the values can also be separated by other strings.
If the macro `$forsep` is defined, like in

{%sample/
{@define $forsep=\s+}
%}

then the arguments will be separated by one or more spaces.
The string between the `(` and the `)` will be split using the string defined in `$forsep` as a regular expression.

Similarly, if the macro `$forsubsep` is defined, like in

{%sample/
{@define $forsubsep=:}
%}

then the values for the different variables will be separated by a semicolon.

Starting with version {%RELEASE:SZIKSZO%} you can also define these options locally using the format

{%sample/
{@for [options] x in (a,b,c)=...}
%}

where the options can be

* `$forsep`, `separator` or `sep` to specify the separator regular expression
* `$forsubsep`, `subseparator` to specify the sub separator regular expression
* `trimForValues`, `trim` to trim off the spaces from the values
* `skipForEmpty`, `skipEmpty` to skip empty parameter list (see below)
* `lenient` for lenient operation (see below)
* `evaluateValueList`, or `evalist` to instruct the loop that the list of the values between the `(` and `)` has to be evaluated.
Not the variables, only the values.

For example, the macros:

{%sample/
{@for [sep=:]$a in (a:b:c)=a is $a
}
%}

will result

{%output%}

Another example:

{%sample/
{@for [trim sep=":"] $a in ( a : b :c )=a is $a
}
%}

will result the same output:

{%output%}

The number of the actual values separated by `|` character should be the same as the number of the variables in the for loop.
If this is not the case, then the macro evaluation will throw a bad syntax exception.
This can be suppressed with the option `lenient`.
If the option `lenient` is used, then extra values are ignored and missing values are presented as empty strings.
Note that this same option controls how user defined macro arguments are paired to the parameters.

Starting with version {%RELEASE:BUELACH%} you can fine-tune how a `for` loop treats the empty elements.
By default, the empty elements in a for loop value list represent empty strings.
The loop body will be rendered with these values replacing the loop variable with an empty string.
In a situation like that the use of the option `lenient` is also necessary if the loop has multiple variables.
In that case, and empty value cannot be split into multiple strings.
It is one empty string, and if there are multiple variables, then an error will occur unless the option or parop lenient is used.
For example

{%sample/
{#for (k,z) in ()=wukz}
%}

it will not work because the empty string cannot be split into two strings.
It results in one empty string when it is split.
On the other hand, the following code will work

{%sample/
{@for [lenient] (k,z) in ()=wukz}
%}

and it will result

{%output%}

as both `k` and `z` are empty strings.

This default behaviour can be altered using the option `skipForEmpty`.
If this option is used the `for` loop will skip the empty values.
The previous example with this option:

{%sample/
{@for [skipEmpty] (k,z) in ()=wukz}\
%}

will evaluate to an empty string.
Also note that in this case there is no need to use the option `lenient`.
That is because the empty value is skipped and there is no issue splitting it up into a lesser number of values than the number of the loop variables.

The example above contains one loop value, and that loop value is an empty string.
There can be multiple empty values in a for loop and empty and non-empty values can be mixed.
The option `skipForEmpty` and the alias `skipEmpty` works in any of those cases.
For example:

{%sample/
{@for [skipEmpty] k in (,)=wuk}
%}

will also result an empty string and

{%sample/
{#for k in (,k)=wuk{@options skipForEmpty}}
%}

will result

{%output%}

Sometimes the values for the `for` loop come from some macro.
In that case the `for` macro should start with the `#` character, otherwise the macro will not be evaluated to the list of values.
For example: {%#block[] make sure that x, y, and z are undefined before we start the next sample
{%sample/{@undefine x}{@undefine y}{@undefine z}%}{%output%}%}

{%sample/
{@define list=x,y,z}{@for z in ({list})={@define z=zz}}{?x}{?y}{?z}
%}

will result

{%output%}

That is because the content of the macro `for` is not evaluated before the `for` loop is executed because we used the `@` character.
The result of the for loop is not evaluated.
We will have to attend to that, but first we have to solve the issue that the macro `list` is not evaluated.
To do that we need to use the `#` character in front of the `for` loop.

{%sample/
{@define list=x,y,z}{#for z in ({list})={@define z=zz}}{?x}{?y}{?z}
%}

will result an empty string:

{%output%}

The reason is that the content of the `for` macro is evaluated before executing the macro itself.
That way the macro reference `{list}` will become `x,y,z`, but the same time the part, which is after the `=` is also evaluated.
The evaluation will define the macro `z` to be `zz`, but this macro is within the scope of the `for` macro.
As soon as the `for` macro execution is finished the definition of `z` is lost.
What we want is to protect the body of the `for` macro from evaluation before `for` the macro is executed, and we want it to execute after.

{%sample/
{@define list=x,y,z}{!#for z in ({list})={@ident {@define z=zz}}}{?x}{?y}{?z}
%}

will result

{%output%}

The macro `{@ident ...}` is evaluated, and its result is the content of the macro, and it is not evaluated further before the evaluation of the macro `for`.
The macro `for` gets evaluated and, then the output is evaluated because the macro is preceeded with the `!` character, which is a shorthand for the core built-in macro `eval`.
This evaluation defines `x`, `y` and `z`.

Because the case that we want to evaluate the list part of the `for` loop but not the body part is so common there is an option that helps with this.
The option `evaluateValueList` (alias `evalist`) instructs the macro `for` to evaluate the value list before iterating through it.

{%sample/
{@define list=x,y,z}{!@for [evaluateValueList] z in ({list})={@define z=zz}}{?x}{?y}{?z}
%}

will result

{%output%}

We still need the `!` character in front of the `for` but we could get rid of the `ident` macro and the extra level of nesting.

[NOTE]
====
The use of `evalist` and using `pass:[#]` along with `ident` is not exactly the same.
Using `#` will evaluate the part not protected by `ident` before the `for` macro evaluates its input.
The option `evalist` tells the macro to evaluate the string it has already found that time between the opening `(` and closing `)`.

The consequence is that using `evalist` you can have a list that contains the `)` character.
The end of the list was already determined when the evaluation starts.
Using `#` in front of the macro identifier will cause problem if the list contains the `)` character.

In situations like that you can use the special list separator that we discuss in the next paragraph.
====

Sometimes you may need to do a for loop over values that contain the `)` character.
With the conventional form of the `for` macro it was not possible, because the first `)` character terminates the list of the values.
Jamal 1.6.3 introduced a new, backward compatible format for the `for` macro.

Instead of the `(` and `)` characters it is possible to use an arbitrary string to denote the end of the values.
When the first character after the keyword `in` (after optional spaces) is the backtick character, then the string till the next backtick character will be used to denote the end of the values.
The starting and ending backtick should also be part of the string closing the values.

For example, the following

{%sample/
{@for x in `END`a),b),c),d)`END`=x }
%}

will result

{%output%}

Note that this alternative format can only be used for the values list and not for the variables.
The variables of the for loop should always be listed between `(` and `)` characters.
